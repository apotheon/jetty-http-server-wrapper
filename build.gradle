apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.palominolabs.http'
version = '1.1.1-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

sourceCompatibility = JavaVersion.VERSION_1_7

ext {
  depVersions = [
      slf4j: '1.7.12',
      guice: '4.0'
  ]
}

dependencies {
  compile 'com.google.code.findbugs:jsr305:3.0.1'
  compile 'com.google.guava:guava:18.0'

  compile "org.slf4j:slf4j-api:$depVersions.slf4j"
  testRuntime "org.slf4j:slf4j-simple:$depVersions.slf4j"
  testRuntime "org.slf4j:log4j-over-slf4j:$depVersions.slf4j"
  testRuntime "org.slf4j:jcl-over-slf4j:$depVersions.slf4j"
  testCompile "org.slf4j:jul-to-slf4j:$depVersions.slf4j"

  compile 'org.eclipse.jetty:jetty-servlet:9.3.5.v20151012'

  compile "com.google.inject.extensions:guice-servlet:$depVersions.guice"
  compile "com.google.inject.extensions:guice-assistedinject:$depVersions.guice"
  compile 'ch.qos.logback:logback-access:1.1.3'

  testCompile 'com.palominolabs.config:config-inject:1.0.1'

  testCompile 'org.apache.httpcomponents:httpclient:4.3.3'
  testCompile 'org.easymock:easymock:3.3'

  testCompile 'junit:junit:4.12'
}

configurations {
  all*.exclude group: 'commons-logging', module: 'commons-logging'
}

repositories {
  jcenter()
}

task sourcesJar(type: Jar, dependsOn: classes) {
  from sourceSets.main.allJava
  classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  from javadoc.destinationDir
  classifier = 'javadoc'
}

artifacts {
  // register in 'archives' config so they will be signed
  archives sourcesJar
  archives javadocJar
}

signing {
  required { gradle.taskGraph.hasTask("uploadArchives") }
  sign configurations.archives
}

// Set sonatypeUsername and sonatypePassword in your ~/.gradle/gradle.properties to be able to use this.
uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { deployment -> signing.signPom(deployment) }

      repository(url: isReleaseVersion ?
        "https://oss.sonatype.org/service/local/staging/deploy/maven2/" :
        "https://oss.sonatype.org/content/repositories/snapshots/") {
        def u = project.hasProperty('sonatypeUsername') ? sonatypeUsername : 'FIXME'
        def p = project.hasProperty('sonatypePassword') ? sonatypePassword : 'FIXME'
        authentication(userName: u, password: p)
      }

      pom.project {
        name 'jetty-http-server-wrapper'
        packaging 'jar'
        description 'Wrapper around the Jetty HTTP server to make common configuration easier.'
        url 'https://github.com/palominolabs/jetty-http-server-wrapper'

        scm {
          url 'scm:git@github.com:palominolabs/jetty-http-server-wrapper.git'
          connection 'scm:git@github.com:palominolabs/jetty-http-server-wrapper.git'
          developerConnection 'scm:git@github.com:palominolabs/jetty-http-server-wrapper.git'
        }

        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
          }
        }

        developers {
          developer {
            id 'marshallpierce'
            name 'Marshall Pierce'
          }
        }
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.0'
}
